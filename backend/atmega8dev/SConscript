import os.path
from py.scons.utils import *

Import('env', 'sources', 'node')

programmer = 'usbasp'
avrdude = 'avrdude'
device = 'atmega8'

cflags = {
  'DF_CPU': 16000000,
  'mmcu': device
  }

env['CC'] = 'avr-gcc'
env.AppendUnique(CPPPATH=['.'])
env.Append(CFLAGS=create_ext_opts_str(cflags))

bld = Builder(action = 'avr-objcopy -j .text -j .data -O ihex $SOURCE $TARGET',
        suffix = '.hex',
        src_suffix = '.elf')

env.Append(BUILDERS = {'Hex' : bld})

backend_sources =  env.Glob('*.c')
backend_objects = []

objs_lib = targets_from_src_list(sources['lib'], 'lib', env.Object)
objs_node = targets_from_src_list(sources['node'], 'nodes', env.Object)
objs_backend = targets_from_src_list(backend_sources, 'backend', env.Object)

node_elf = env.Program(target = node + '.elf', source = objs_backend + objs_lib + objs_node)
tg = env.Hex(target = node + '.hex', source = node_elf)

# flash commands

AddOption('--flash', action='store_true', dest='flash')

if GetOption('flash'):
  target = COMMAND_LINE_TARGETS[0]

  if target.endswith('.hex'):
    print "target: " + target
    print "flash"*23
    cmd = "{0} -c {1} -p {2} -U flash:w:{3}:i".format(avrdude, programmer, device, target)
    env.AddPostAction(tg, cmd)
    env.AlwaysBuild(target)
